plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.kotlin.serialization)
}

android {
    namespace = 'org.godotengine.godot_gradle_build_environment'
    compileSdk = 35
    ndkVersion = '28.1.13356709'

    def keystoreFile = System.getenv("GABE_RELEASE_KEYSTORE_FILE")

    sourceSets {
        main {
            jniLibs.srcDirs += [
                '../thirdparty/proot/build/bin',
            ]
        }
    }

    defaultConfig {
        applicationId = "org.godotengine.godot_gradle_build_environment"
        minSdk = 24
        targetSdk = 35
        versionCode = 2
        versionName = "0.2"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    if (keystoreFile != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("GABE_RELEASE_KEYSTORE_FILE"))
                storePassword System.getenv("GABE_RELEASE_SIGNING_PASSWORD")
                keyAlias System.getenv("GABE_RELEASE_KEY_ALIAS")
                keyPassword System.getenv("GABE_RELEASE_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        release {
            if (keystoreFile != null) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

    buildFeatures {
        compose true
        buildConfig true
    }
}

dependencies {
    implementation(libs.commons.compress)
    implementation(libs.xz)
    implementation(libs.kotlinx.serialization.json)
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.ui.graphics
    implementation libs.androidx.compose.ui.tooling.preview
    implementation libs.androidx.compose.material3
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.compose.ui.test.junit4
    debugImplementation libs.androidx.compose.ui.tooling
    debugImplementation libs.androidx.compose.ui.test.manifest
}